dnl
dnl

AC_PREREQ(2.50)

AC_INIT(org.eclipse.ptp.debug.sdm, 1.0.0)
AC_CONFIG_SRCDIR(src/client/client.c)

AM_INIT_AUTOMAKE([subdir-objects])

AC_CONFIG_HEADER(config.h)
AC_CANONICAL_BUILD

CFLAGS="$CFLAGS -Wall"

dnl
dnl Check for programs.
dnl

AC_PROG_CC([mpicc])
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_LIBTOOL

dnl
dnl Check for libraries.
dnl

AC_CHECK_LIB(m, log2l)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)
AC_SEARCH_LIBS(openpty, [util],
	[AC_DEFINE(HAVE_OPENPTY, 1, [System has openpty() function.])])dnl

dnl AIF libraries
dnl AC_TEST_PACKAGE_NEW(aif,[#include <aif.h>],-laif,,[/usr/local $(TOP)/../libaif],LIBAIF)
dnl if test "$with_aif" = "no"; then
dnl 	INCLUDE_aif='$(srcdir)/src/aif'
dnl 	LIB_aif='$(srcdir)/src/aif'
dnl 	# note: copy of libaif in guard src tree is old, and should not
dnl 	# be used anymore.
dnl 	AC_MSG_ERROR([You must have libaif to compile sdm.])
dnl fi

dnl Check for posix4
AC_CHECK_LIB(posix4, clock_gettime)
if test "$ac_cv_lib_posix4_clock_gettime" = yes; then
	AC_DEFINE(POSIX, 1, [Enable POSIX support in Guard.])
	LIBS="$LIBS -lposix4"
fi

dnl
dnl Check for header files.
dnl

AC_HEADER_STDC
AC_CHECK_HEADERS(strings.h sys/time.h unistd.h)
AC_CHECK_HEADERS(jni.h)
AM_CONDITIONAL(PTPDBGJNI, [test "$ac_cv_header_jni_h" = yes])

dnl
dnl Check for typedefs, structures, and compiler characteristics.
dnl

AC_HEADER_TIME
AC_STRUCT_TM

dnl
dnl Check for library functions.
dnl

AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname select strdup strstr strtoul sigset sighold sigrelse)

dnl Open MPI
AC_PATH_PROG(OMPI_INFO, ompi_info, no)
if test "$ac_cv_path_OMPI_INFO" != no; then
	AC_PATH_PROG(MPIRUN, mpirun, no)
	if test "$ac_cv_path_MPIRUN" != no ; then
		AC_DEFINE(OMPI,1,[Enable Open MPI support.])
		AC_DEFINE_UNQUOTED(MPIRUN, "$ac_cv_path_MPIRUN", [Location of mpirun command.])
	fi
fi

AC_CONFIG_FILES([Makefile])
AC_CONFIG_SUBDIRS([bitvector libaif ../org.eclipse.ptp.proxy ../org.eclipse.ptp.utils])
AC_OUTPUT
